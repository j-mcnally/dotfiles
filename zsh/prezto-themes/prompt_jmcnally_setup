#
# A theme based on steeef theme
#  * RVM/Rbenv info shown on the right
#  * Git branch info on the left
#  * Single line prompt
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Yan Pritzker <yan@pritzker.ws>

function prompt_jmcnally_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info on
    git-info
  fi

  # Get ruby information
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi
}

function prompt_jmcnally_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_jmcnally_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    __PROMPT_jmcnally_COLORS=(
      "%F{6}"  # turquoise
      "%F{160}" # orange
      "%F{20}" # purple
      "%F{124}" # hotpink
      "%F{92}" # limegreen
    )
  else
    __PROMPT_jmcnally_COLORS=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # git
  zstyle ':prezto:module:git:info:branch' format "${__PROMPT_jmcnally_COLORS[1]}%b%f"
  zstyle ':prezto:module:git:info:added' format "${__PROMPT_jmcnally_COLORS[5]}●%f"
  zstyle ':prezto:module:git:info:deleted' format "${__PROMPT_jmcnally_COLORS[2]}●%f"
  zstyle ':prezto:module:git:info:modified' format "${__PROMPT_jmcnally_COLORS[4]}●%f"
  zstyle ':prezto:module:git:info:untracked' format "${__PROMPT_jmcnally_COLORS[3]}●%f"
  zstyle ':prezto:module:git:info:keys' format 'prompt' '(%b%d%a%m%u)'

  # ruby info (rvm, rbenv)
  zstyle ':prezto:module:ruby:info:version' format '[%v]'

  PROMPT="${__PROMPT_jmcnally_COLORS[3]}%n%f@${__PROMPT_jmcnally_COLORS[2]}%m%f ${__PROMPT_jmcnally_COLORS[5]}%~%f "'$git_info[prompt]'"$ "
  RPROMPT='%F{blue}${ruby_info[version]}'
}

prompt_jmcnally_setup "$@"
